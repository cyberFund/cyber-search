/**
 *
 * Steps:
 *  1) For each module during testing generate test result (.exec file)
 *  2) Merge all .exec file into single root allTestCoverageFile .exec file
 *  3) Generate from root .exec file xml and html reports
 *  4) Send to coverall.io generate root xml report file
 *
 */

apply plugin: 'com.github.kt3k.coveralls'
apply plugin: "jacoco"

repositories {
    jcenter()
}

jacoco {
    toolVersion = "0.8.2"
}

def allTestCoverageFile = "$buildDir/jacoco/rootTestsCoverage.exec"

task jacocoMergeSubprojectResultsIntoRootOne(type: JacocoMerge) {
    destinationFile = file(allTestCoverageFile)
    executionData = project.fileTree(dir: '.', include: '**/build/jacoco/moduleTestsCoverage.exec')
}

task jacocoMerge(dependsOn: ['jacocoMergeSubprojectResultsIntoRootOne'])

/* Used to generate root(merged) html and xml coverage files from previously merged exec*/
task jacocoRootReport(type: JacocoReport, dependsOn: "jacocoMerge") {
    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    executionData = files(allTestCoverageFile)
}

coveralls {
    sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
}

tasks.coveralls {
    dependsOn(jacocoRootReport)
}
